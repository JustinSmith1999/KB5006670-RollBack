@echo off

call :_getostype %osarch
echo Rolling Back Windows 10 Spooler %osarch% files from %~dp0

for /f "tokens=2 delims=," %%i in ('wmic os get version /format:csv') do set osver=%%i
for /f "tokens=1 delims=." %%i in ("%osver%") do set osver=%%i
IF NOT "%osver%" == "10" goto :_wrongos

NET SESSION >nul 2>&1
IF NOT %ERRORLEVEL% EQU 0 goto :_norights

set "osdir=%WINDIR%\system32\"

call :_getversion "%osdir%localspl.dll" %localsplver
call :_getversion "%osdir%win32spl.dll" %win32splver
call :_getversion "%osdir%spoolsv.exe" %spoolsvver

if not "%localsplver%"=="10.0.19041.1526" goto :_end
if not "%spoolsvver%"=="10.0.19041.1237" goto :_end
if not "%win32splver%"=="10.0.19041.1237" goto :_end
echo System Needs to be reverted
goto :_canrevert
goto :_end

:_canrevert 
if not exist "%osdir%win32spl.dll-10.0.19041.1387" goto :_end
if not exist "%osdir%spoolsv.exe-10.0.19041.1415" goto :_end
echo System Can Revert
goto :_revert
goto :_end

:_revert 
echo Reverting Windows 10 Spooler %osarch% files from %~dp0
call :_swapback "win32spl.dll-10.0.19041.1387" "win32spl.dll" "win32spl.dll-10.0.19041.1237"
call :_swapback "spoolsv.exe-10.0.19041.1415" "spoolsv.exe" "spoolsv.exe-10.0.19041.1237"
echo System Reverted
goto :_end

:_swapback
echo Swapping "%osdir%%~2" to "%~3"
IF NOT "%spooler_stopped%" == "yes" call :_stop_spooler
call :_take_control "%osdir%%~2"
IF EXIST "%osdir%%~3" del "%osdir%%~3"
ren  "%osdir%%~2" "%~3"
ren  "%osdir%%~1" "%~2"
echo Swap Completed
exit /b

:_stop_spooler
echo Stopping Spooler
net stop spooler
timeout /t 3 /nobreak > nul
set "spooler_stopped=yes"
exit /b

:_start_spooler
echo Starting Spooler
net start spooler
timeout /t 3 /nobreak > nul
set "spooler_stopped=no" 
exit /b

:_take_control
echo Taking Control of "%~1"
Takeown /A /F "%~1"
icacls  "%~1" /grant builtin\administrators:F
icacls  "%~1" /grant SYSTEM:F
exit /b

:_getversion  
SETLOCAL
set "file=%~1"
set "item1=%file:\=\\%"
for /f "usebackq delims=" %%a in (`"WMIC DATAFILE WHERE name='%item1%' get Version /format:Textvaluelist"`) do (
    for /f "delims=" %%# in ("%%a") do set "%%#")
ENDLOCAL&set %~2=%version%
exit /b

:_getostype  
SETLOCAL
for /f "usebackq delims=" %%a in (`"WMIC os get OSArchitecture /format:Textvaluelist"`) do (
    for /f "delims=" %%# in ("%%a") do set "%%#")
ENDLOCAL&set %~1=%OSArchitecture%
exit /b

:_swap
echo Swapping "%~1%~2" to "%~3"
IF NOT "%spooler_stopped%" == "yes" call :_stop_spooler
call :_take_control "%~1%~2"
IF EXIST "%~2-%~3" del "%~2-%~3"
ren  "%~1%~2" "%~2-%~3"
ren  "%~1%~2.good" "%~2"
echo Swap Completed
exit /b

:_wrongos
echo This script was intended for Windows 10.
echo It can be used on Server's but you need to source the correct bins.
goto :_exit

:_norights
echo Admin Privileges required.
pause
goto :_exit

:_end
IF "%spooler_stopped%" == "yes" call :_start_spooler
echo Rolling Back Spooler files Complete.
goto :_exit
:_exit
